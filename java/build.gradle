/*
 * Copyright 2019 Regents of the University of Minnesota.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
}

group 'edu.umn.biomedicus'

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().replaceFirst('^v', "").trim()
}

version gitVersion()

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenCentral()
    mavenLocal()
}

def junitVersion = '5.3.1'
def slf4jVersion = '1.7.26'
def jaxbVersion = '2.3.2'

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '17.0.0'

    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    implementation group: 'edu.umn.nlpie', name: 'mtap', version: '[0.1.0, )'
    implementation group: 'edu.umn.biomedicus', name: 'biomedicus-tokenizer', version: '0.0.3'
    implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '6.0.1'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'

    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: jaxbVersion
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    implementation group: 'org.simpleframework', name: 'simple-xml', version: '2.7.1'

    runtimeOnly group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbVersion
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.2'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

test {
    useJUnitPlatform()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'NLP-NEWT',
                'Implementation-Version': version,
                'Main-Class': 'edu.umn.nlpie.mtap.MTAP'
    }
    setArchiveBaseName project.name + '-all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task execute(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = project.hasProperty('mainClass') ? project.getProperties().get('mainClass') : null
}

task conceptsUtility(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main 'edu.umn.biomedicus.concepts.ConceptsUtility'
}
